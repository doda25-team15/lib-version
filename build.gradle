import nl.tudelft.libversion.buildscript.GenerateBuildInfoTask
import java.text.SimpleDateFormat

plugins {
    id 'java-library'
    id 'maven-publish'
}

def branch = System.getenv("GITHUB_REF_NAME") ?: "local"
def isMain = branch == "main"

// baseVersion comes from gradle.properties (required!)
def baseVersion = project.findProperty("baseVersion") ?: "1.0.0"

// timestamp for strictly increasing pre-releases
def timestamp() {
    return new SimpleDateFormat("yyMMdd-HHmmss").format(new Date())
}

if (isMain) {
    // main branch always has SNAPSHOT
    project.ext.calculatedVersion = "${baseVersion}-SNAPSHOT"
} else {
    // safe branch name
    def safeBranch = branch.replaceAll("[^A-Za-z0-9-]", "-")
    project.ext.calculatedVersion = "${baseVersion}-${safeBranch}-${timestamp()}"
}

base {
    archivesName = project.archives_base_name
    // <<< IMPORTANT: use the calculated dynamic version
    version = project.calculatedVersion
    group = project.maven_group
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "com.google.code.gson:gson:${project.gson_version}"
    implementation "com.google.guava:guava:${project.guava_version}-jre"
}

tasks.register('generateBuildInfo', GenerateBuildInfoTask) {
    outputFile = new File(temporaryDir, "build_info.json")
}

processResources {
    from("LICENSE") {
        rename {"${it}_${project.base.archivesName.get()}"}
    }
    from generateBuildInfo
}

testing {
    suites {
        test {
            useJUnitJupiter(project.junit_version)
        }
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }

    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = project.base.archivesName.get()
            from components.java
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/doda25-team15/lib-version"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}